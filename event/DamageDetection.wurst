package DamageDetection
import MapBounds
import OnUnitEnterLeave

constant SWAP_TIMEOUT = 600.
constant filter = Filter(() -> current.registerUnitEvent(GetFilterUnit(), EVENT_UNIT_DAMAGED))

boolexpr array func
integer funcNext = 0
trigger current = null
trigger toDestroy = null

/** Add a Condition(Action) to the damage Event. */
public function addOnDamageFunc(boolexpr cf)
	current.addCondition(cf)
	func[funcNext] = cf
	funcNext++

/** Disables the damage events */
function disableDamageDetect()
	DisableTrigger(current)

/** Enables the damage events */
function enableDamageDetect()
	EnableTrigger(current)


/** Periodic Cleanup-Function */
function swap()
	boolean b = IsTriggerEnabled(current)

	current.disable()
	if toDestroy != null
		toDestroy.destr()

	toDestroy = current
	current = CreateTrigger()

	if not b
		current.disable()


	GroupEnumUnitsInRect(ENUM_GROUP, boundRect, filter)

	for i = 0 to funcNext-1
		current.addCondition(func[i])


init
	current = CreateTrigger()
	for i = 0 to funcNext-1
		current.addCondition(func[i])
	onEnter(() -> filter)
	GroupEnumUnitsInRect(ENUM_GROUP, boundRect, filter)
	CreateTimer().startPeriodic(SWAP_TIMEOUT, function swap)
