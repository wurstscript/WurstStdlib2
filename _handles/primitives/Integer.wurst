package Integer
import NoWurst
import Real

/** Returns the absolute value of this int */
public function int.abs() returns int
	if this >= 0
		return this
	else
		return -this

/** Returns the square of this int */
public function int.squared() returns int
	return this*this
		
/** returns the sign of the int */
public function int.sign() returns int
	if this > 0
		return 1
	else if this < 0
		return -1
	else
		return 0

/** Returns the int as real */
public function int.toReal() returns real
	return this * 1.

/** Returns the string representation of this int */
public function int.toString() returns string
	return I2S(this)

/** Returns this int to the power of the argument int */
public function int.pow(int x) returns int
	int result = 1
	for int i=1 to x
		result *= this
	return result

/** Linear Interpolation with alphafactor(smoothness) */
public function int.lerp(int target, real alpha) returns int
	return ((this * (1-alpha)) + (target * alpha)).round()