package GroupTests

@Test
function testGroupBasic()
	let g = CreateGroup()
	assertTrue(g != null)
	assertTrue(g.isEmpty())

	let u = createUnit(Player(0), 'hfoo', vec2(0, 0), 0 .fromDeg())
	assertTrue(u != null)
	g.addUnit(u)

	assertTrue(g.size() == 1)
	for i = 1 to 10
		g.addUnit(createUnit(Player(0), 'hfoo', vec2(0, 0), 0 .fromDeg()))

	assertTrue(g.size() == 11)


	let u2 = createUnit(Player(0), 'hfoo', vec2(0, 0), 0 .fromDeg())
	g.addUnit(u2)
	assertTrue(g.getUnitAt(0) == u)
	assertTrue(g.size() == 12)
	assertTrue(g.getUnitAt(g.size()-1) == u2)

	for int i = 0 to g.size()-1
		print(g.getUnitAt(i).getHandleId())
	print("---")
	print(u2.getHandleId())

@Test
function testGroupForLoops()
	let g = CreateGroup()
	assertTrue(g != null)
	assertTrue(not g.hasNext())
	for i = 0 to 10
		g.addUnit(createUnit(Player(0), 'hfoo', vec2(0, 0), 0 .fromDeg()))

	assertTrue(g.hasNext())

	for u in g
		assertTrue(u != null)

	for _ from g

	assertTrue(not g.hasNext())


@Test
function testRandom()
	let g = CreateGroup()

	for i = 0 to 10
		g.addUnit(createUnit(Player(0), 'hfoo', vec2(0, 0), 0 .fromDeg()))

	let ru = g.getRandomUnit()

	assertTrue(ru != null)

	assertTrue(g.contains(ru))

	g.removeUnit(ru)

	for i = 1 to 10
		g.removeUnit(g.getRandomUnit())

	assertTrue(g.isEmpty())

@Test
function testHandleId()
	let g = CreateGroup()
	assertTrue(g.getHandleId() != 0)

	for i = 0 to 100
		let g2 = CreateGroup()
		assertTrue(g2.getHandleId() != 0)
		assertTrue(g2.getHandleId() != g.getHandleId())

@Test
function testVararg()
	let g = CreateGroup()
	g.addUnit(createUnit(Player(0), 'hfoo', vec2(0, 0), 0 .fromDeg()), createUnit(Player(0), 'hfoo', vec2(0, 0), 0 .fromDeg()), createUnit(Player(0), 'hfoo', vec2(0, 0), 0 .fromDeg()))
	g.size().assertEquals(3)

@Test
function testGroupForGroups()
	let u = createUnit(Player(0), 'hfoo', vec2(0, 0), 0 .fromDeg())
	let u2 = createUnit(Player(0), 'hfoo', vec2(0, 0), 0 .fromDeg())
	let u3 = createUnit(Player(0), 'hfoo', vec2(0, 0), 0 .fromDeg())

	let g = CreateGroup()
	g.addUnit(u, u2)
	g.size().assertEquals(2)

	let g2 = CreateGroup()
	g2.addGroup(g)
	g2.size().assertEquals(g.size())
	g2.contains(u).assertTrue()

	g2.addUnit(u3)
	g2.removeGroup(g)
	g2.size().assertEquals(1)
	g2.contains(u3).assertTrue()
	g2.contains(u2).assertFalse()
