package Multiboard
import NoWurst
import _Primitives
import Colors
import Player
import Annotations

public function multiboard.destr()
	DestroyMultiboard(this)

public function multiboard.isDisplayed() returns boolean
	return IsMultiboardDisplayed(this)

public function multiboard.isMinimized() returns boolean
	return IsMultiboardMinimized(this)

public function multiboard.isMaximized() returns boolean
	return not this.isMinimized()

public function multiboard.clear()
	MultiboardClear(this)

public function multiboard.display(boolean show)
	MultiboardDisplay(this, show)

public function multiboard.display(player p, boolean show)
	if localPlayer == p
		MultiboardDisplay(this,show)

public function multiboard.show()
	this.display(true)

public function multiboard.show(player p)
	if localPlayer == p
		this.display(true)

public function multiboard.hide()
	this.display(false)

public function multiboard.hide(player p)
	if localPlayer == p
		this.display(false)

public function multiboard.getColumnCount() returns int
	return MultiboardGetColumnCount(this)

public function multiboard.getItem(int row, int column) returns multiboarditem
	return MultiboardGetItem(this,row,column)

public function multiboard.getRowCount() returns int
	return MultiboardGetRowCount(this)

public function multiboard.getTitleText() returns string
	return MultiboardGetTitleText(this)

@deprecated("Use .minimize() instead")
public function multiboard.minimalize(boolean minimalize)
	MultiboardMinimize(this, minimalize)

public function multiboard.minimize()
	MultiboardMinimize(this, true)

public function multiboard.minimize(player p)
	if localPlayer == p
		this.minimize()

public function multiboard.maximize()
	MultiboardMinimize(this, true)

public function multiboard.maximize(player p)
	if localPlayer == p
		this.maximize()

public function multiboard.setColumnCount(int count)
	MultiboardSetColumnCount(this, count)

public function multiboard.setRowCount(int count)
	MultiboardSetRowCount(this, count)

public function multiboard.setItemsIcon(string path)
	MultiboardSetItemsIcon(this, path)

public function multiboard.setItemsStyle(boolean showValues, boolean showIcons)
	MultiboardSetItemsStyle(this, showValues, showIcons)

public function multiboard.setItemsValue(string value)
	MultiboardSetItemsValue(this,value)

public function multiboard.setItemsValueColor(int r,int g, int b, int a)
	MultiboardSetItemsValueColor(this,r,g,b,a)

public function multiboard.setItemsWidth(real width)
	MultiboardSetItemsWidth(this, width)

public function multiboard.setTitle(string label)
	MultiboardSetTitleText(this, label)

public function multiboard.setTitleColor(int r,int g, int b, int a)
	MultiboardSetTitleTextColor(this, r,g,b,a)

public function multiboard.setTitleColor(colorA col)
	MultiboardSetTitleTextColor(this, col.red, col.green, col.blue, col.alpha)

public function multiboard.setItemValue(int row, int column, string value)
	this.getItem(row, column)
	..setValue(value)
	..release()

public function multiboard.setItemValue(int row, int column, int value)
	this.setItemValue(row, column, value.toString())

public function multiboard.setItemValue(int row, int column, real value)
	this.setItemValue(row, column, value.toString())

public function multiboard.setItemValue(int row, int column, bool value)
	this.setItemValue(row, column, value.toString())

public function multiboard.setItemValueColor(int row, int column, int r, int g, int b, int a)
	this.getItem(row, column)
	..setValueColor(r, g, b, a)
	..release()

public function multiboard.setItemValueColor(int row, int column, colorA col)
	this.setItemValueColor(row, column, col.red, col.green, col.blue, col.alpha)

public function multiboard.setItemIcon(int row, int column, string iconFileName)
	this.getItem(row, column)
	..setIcon(iconFileName)
	..release()

public function multiboard.setItemWidth(int row, int column, real width)
	this.getItem(row, column)
	..setWidth(width)
	..release()

public function multiboard.setColumnWidth(int column, real width)
	if column >= this.getColumnCount()
		return
	for row = 0 to this.getRowCount() - 1
		this.getItem(row, column)
		..setWidth(width)
		..release()

public function multiboard.setRowWidth(int row, real width)
	if row >= this.getRowCount()
		return
	for column = 0 to this.getColumnCount() - 1
		this.getItem(row, column)
		..setWidth(width)
		..release()

// multiboarditems
public function multiboarditem.release()
	MultiboardReleaseItem(this)

public function multiboarditem.setIcon(string iconFileName)
	let v = iconFileName == null ? "" : iconFileName
	MultiboardSetItemIcon(this, v)

public function multiboarditem.setStyle(boolean showValue, boolean showIcon)
	MultiboardSetItemStyle(this, showValue, showIcon)

public function multiboarditem.setValue(string value)
	let v = value == null ? "" : value
	MultiboardSetItemValue(this, v)

public function multiboarditem.setValue(int value)
	this.setValue(value.toString())

public function multiboarditem.setValue(real value)
	this.setValue(value.toString())

public function multiboarditem.setValue(bool value)
	this.setValue(value.toString())

public function multiboarditem.setValueColor(int r, int g, int b, int a)
	MultiboardSetItemValueColor(this, r, g, b, a)

public function multiboarditem.setValueColor(colorA col)
	MultiboardSetItemValueColor(this, col.red, col.green, col.blue, col.alpha)

public function multiboarditem.setWidth(real width)
	MultiboardSetItemWidth(this, width)
